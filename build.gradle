plugins {
	id 'java'
	id 'org.spongepowered.plugin' version '0.9.0'
	id 'com.github.johnrengelman.shadow' version '6.1.0'
	id 'org.jetbrains.kotlin.jvm' version '1.4.21'
	id 'org.jetbrains.kotlin.kapt' version '1.4.21'
}

group = 'io.github.boogiemonster1o1'
version = '0.1.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

logger.lifecycle("Building ${rootProject.name} version ${project.version}")

repositories {
	mavenCentral()
	jcenter()
	maven {
		name = 'Sponge Maven'
		url = 'https://repo.spongepowered.org/maven'
	}
	maven {
		name = 'Jitpack'
		url = "https://jitpack.io"
	}
}

dependencies {
	compile 'com.github.jearmstrong21:PESL:36498a1'
	compile 'org.spongepowered:spongeapi:7.3.0'
	annotationProcessor 'org.spongepowered:spongeapi:7.3.0'
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

shadowJar {
	exclude 'module-info.class'
	dependencies {
		include(dependency('com.github.jearmstrong21:PESL:36498a1'))
		include(dependency('org.jetbrains.kotlin:kotlin-stdlib-jdk8'))
	}
	relocate 'p0nki.pesl', 'io.github.boogiemonster1o1.palpebratingpesl.shaded.p0nki.pesl'
	relocate 'kotlin', 'io.github.boogiemonster1o1.palpebratingpesl.shaded.kotlin'
}

task copyPlugin(type: Copy) {
	from "build/libs/${archivesBaseName}-${version}-all.jar"
	into "run/mods"
}

task cleanModsDir {
	doLast {
		def mods = file('run/mods')
		if (mods.exists()) {
			mods.deleteDir()
		}
		mods.mkdir()
	}
}

tasks.build.finalizedBy(sourcesJar, copyPlugin)
tasks.clean.dependsOn(cleanModsDir)

sponge {
	plugin {
		id = 'palpebratingpesl'
		meta {
			name = rootProject.name
			version = project.version
			description = 'PESL Scripting'
		}
	}
}
compileKotlin {
	kotlinOptions {
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		jvmTarget = "1.8"
	}
}
